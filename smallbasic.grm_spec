

========================

Stmt := CR   // Empty statement
     |  ID = Expr
     |  If ( CondExpr ) Then CR Stmts CR MoreThanZeroElseIf  

              // If ( CondExpr ) Then CR Stmts 
              //   { ElseIf ( CondExpr ) Then Stmts }* 
              //   { Else CR Stmts } EndIf

     |  While ( CondExpr ) Then CR Stmts CR EndWhile
     |  ID :
     |  Goto ID
     |  For ID = Expr To Expr OptStep CR Stmts CR EndFor
     |  Sub ID CR Stmts CR EndSub
     |  Expr

MoreThanZeroElseIf
     := OptionalElse
     |  ElseIf ( CondExpr ) Then CR Stmts CR MoreThanZeroElseIf

OptionalElse
     := EndIf
     |  Else CR Stmts CR EndIf

Stmts
     := 
     |  MoreThanOneStmt

MoreThanOneStmt
     := Stmt
     |  Stmt CR MoreThanOneStmt

OptStep 
     :=
     |  Step Expr


========================

CondExpr := OrExpr

OrExpr  := OrExpr Or AndExpr
        |  AndExpr

AndEpxr := AndExpr And EqNeqExpr
        |  EqNeqExpr


EqNeqExpr
        :=  Expr = Expr
        |   Expr <> Expr
        |   CompExpr

CompExpr 
        := Expr < Expr
        |  Expr <= Expr
        |  Expr > Expr
        |  Expr >= Expr
        |  AtomicCondExpr


AtomicCondExpr 
        := ( CondExpr )
        |  ID
        |  "true"
        |  "false"

========================

Expr  := PMExpr

PMExpr := PMExpr + MDExpr
       |  PMExpr - MDExpr
       |  MDExpr

MDExpr := MDExpr * UMExpr
       |  MDExpr / UMExpr
       |  UMExpr

UMExpr := - AtomicExpr
       |  AtomicExpr

AtomicExpr 
       := ( Expr )
       | NUM
       | ID
       | ID . ID
       | ID . ID ( Exprs )
       | ID ( )


Exprs :=        // 비어 있는 것
      |  MoreThanOneExprs

MoreThanOneExprs
      := Expr
      |  Expr , MoreThanOneExprs

========================

